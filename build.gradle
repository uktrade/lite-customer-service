plugins {
  id "java"
  id "application"
  id 'com.github.johnrengelman.shadow' version '1.2.3'
  id "au.com.dius.pact" version "3.3.7"
}

apply plugin: 'idea'

idea {
  module {
    inheritOutputDirs = false
    outputDir = file('build/classes/main')
    testOutputDir = file('build/classes/test')
  }
}

group 'uk.gov.bis.lite'

if (project.hasProperty('projVersion')) {
  project.version = project.projVersion
} else {
  project.version = "1.0-SNAPSHOT"
}

mainClassName = "uk.gov.bis.lite.customer.CustomerApplication"

repositories {
  mavenCentral()
  jcenter()
  maven {
    name "snapshots"
    url "https://nexus.ci.uktrade.io/repository/maven-snapshots/"
  }
  maven {
    name "releases"
    url "https://nexus.ci.uktrade.io/repository/maven-releases/"
  }
}

ext.dropwizardVersion = '1.2.0'

dependencies {
  compile "io.dropwizard:dropwizard-core:$dropwizardVersion"
  compile "io.dropwizard:dropwizard-db:$dropwizardVersion"
  compile "io.dropwizard:dropwizard-jdbi:$dropwizardVersion"
  compile "io.dropwizard:dropwizard-auth:$dropwizardVersion"
  compile "io.dropwizard:dropwizard-client:$dropwizardVersion"
  compile 'uk.gov.bis.lite:spire-client:1.5'
  compile 'uk.gov.bis.lite:jwt:1.2'
  compile 'ru.vyarus:dropwizard-guicey:3.2.0'
  compile 'net.logstash.logback:logstash-logback-encoder:4.10'
  compile 'ch.qos.logback.contrib:logback-jackson:0.1.5'
  compile 'uk.gov.bis.lite:json-console-appender:1.1'
  compile 'uk.gov.bis.lite:jersey-correlation-id:1.0'
  compile 'uk.gov.bis.lite:paas-utils:1.0'

  testCompile "io.dropwizard:dropwizard-testing:$dropwizardVersion"
  testCompile 'org.assertj:assertj-core:3.4.1'
  testCompile group: 'junit', name: 'junit', version: '4.12'

  testCompile group: 'au.com.dius', name: 'pact-jvm-provider-junit_2.11', version: '3.3.7'
}

shadowJar {
  classifier = ''
  mergeServiceFiles()
}

jar {
  manifest {
    attributes 'Main-Class': mainClassName
  }
}

build {
  dependsOn shadowJar
}

run { args 'server', 'config.yaml' }

apply plugin: 'maven-publish'

publishing {
  repositories {
    maven {
      name "snapshots"
      url "https://nexus.ci.uktrade.io/repository/maven-snapshots/"
    }
    maven {
      name "releases"
      url "https://nexus.ci.uktrade.io/repository/maven-releases/"
    }
    maven {
      name "lite-builds"
      url "https://nexus.ci.uktrade.io/repository/lite-builds/"
    }
  }
}

task apiJar(type: Jar, dependsOn: compileJava) {
  from sourceSets.main.output.classesDir
  include 'uk/gov/bis/lite/customer/api/**/*'
}

task apiJarWithSources(type: Jar) {
  from sourceSets.main.allJava
  include 'uk/gov/bis/lite/customer/api/**/*'
}

publishing {
  publications {
    api(MavenPublication) {
      artifactId 'lite-customer-service-api'
      version '1.1'
      artifact apiJar
      artifact apiJarWithSources {
        classifier "sources"
      }
    }
    service(MavenPublication) {
      artifactId 'customer-service'
      from components.shadow
    }
  }
}
