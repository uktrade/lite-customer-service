plugins {
  id "java"
  id "application"
  id 'com.github.johnrengelman.shadow' version '2.0.2'
  id "au.com.dius.pact" version "3.5.13"
  id 'jacoco'
}

apply plugin: 'idea'

idea {
  module {
    inheritOutputDirs = false
    outputDir = file('build/classes/main')
    testOutputDir = file('build/classes/test')
  }
}

group 'uk.gov.bis.lite'

project.version = project.hasProperty('projVersion') ? project.projVersion : '1.0-SNAPSHOT'

mainClassName = "uk.gov.bis.lite.customer.CustomerApplication"

repositories {
  mavenCentral()
  jcenter()
  maven {
    name "snapshots"
    url "https://nexus.ci.uktrade.io/repository/maven-snapshots/"
  }
  maven {
    name "releases"
    url "https://nexus.ci.uktrade.io/repository/maven-releases/"
  }
}

ext.dropwizardVersion = '1.2.9'

dependencies {
  compile "com.fasterxml.jackson.core:jackson-databind:2.9.7"
  compile "io.dropwizard:dropwizard-core:$dropwizardVersion"
  compile "io.dropwizard:dropwizard-db:$dropwizardVersion"
  compile "io.dropwizard:dropwizard-jdbi:$dropwizardVersion"
  compile "io.dropwizard:dropwizard-auth:$dropwizardVersion"
  compile "io.dropwizard:dropwizard-client:$dropwizardVersion"
  compile 'uk.gov.bis.lite:spire-client:1.8'
  compile 'uk.gov.bis.lite:jwt:1.3'
  compile 'uk.gov.bis.lite:lite-user-service-api:1.2'
  compile 'ru.vyarus:dropwizard-guicey:3.2.0'
  compile 'uk.gov.bis.lite:json-console-appender:1.1.1'
  compile 'uk.gov.bis.lite:jersey-correlation-id:1.0'
  compile 'uk.gov.bis.lite:paas-utils:1.0'
  compile 'uk.gov.bis.lite:basic-auth:1.1'
  compile 'uk.gov.bis.lite:redis-cache:0.1.4'

  testCompile "io.dropwizard:dropwizard-testing:$dropwizardVersion"
  testCompile 'org.assertj:assertj-core:3.4.1'
  testCompile group: 'junit', name: 'junit', version: '4.12'

  testCompile 'au.com.dius:pact-jvm-provider-junit_2.11:3.5.13'

}

shadowJar {
  classifier = ''
  mergeServiceFiles()
}

jar {
  manifest {
    attributes 'Main-Class': mainClassName
  }
}

build {
  dependsOn shadowJar
}

run { args 'server', 'config.yaml' }

apply plugin: 'maven-publish'

def nexusUsername = project.hasProperty('nexusUsername') ? project.nexusUsername : ''
def nexusPassword = project.hasProperty('nexusPassword') ? project.nexusPassword : ''

publishing {
  repositories {
    maven {
      name "snapshots"
      url "https://nexus.ci.uktrade.io/repository/maven-snapshots/"
      credentials {
        username nexusUsername
        password nexusPassword
      }
    }
    maven {
      name "releases"
      url "https://nexus.ci.uktrade.io/repository/maven-releases/"
      credentials {
        username nexusUsername
        password nexusPassword
      }
    }
    maven {
      name "lite-builds"
      url "https://nexus.ci.uktrade.io/repository/lite-builds/"
      credentials {
        username nexusUsername
        password nexusPassword
      }
    }
  }
}

task apiJar(type: Jar, dependsOn: compileJava) {
  from sourceSets.main.output.classesDir
  include 'uk/gov/bis/lite/customer/api/**/*'
}

task apiJarWithSources(type: Jar) {
  from sourceSets.main.allJava
  include 'uk/gov/bis/lite/customer/api/**/*'
}

publishing {
  publications {
    api(MavenPublication) {
      artifactId 'lite-customer-service-api'
      version '1.1'
      artifact apiJar
      artifact apiJarWithSources {
        classifier "sources"
      }
    }
    shadow(MavenPublication) { publication ->
      project.shadow.component(publication)
    }
  }
}
