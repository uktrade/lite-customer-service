plugins {
  id "java"
  id "application"
  id 'com.github.johnrengelman.shadow' version '1.2.3'
}

apply plugin: 'idea'
apply plugin: 'maven-publish'

idea {
  module {
    inheritOutputDirs = false
    outputDir = file('build/classes/main')
    testOutputDir = file('build/classes/test')
  }
}

group 'lite-customer-service'
version '1.0-SNAPSHOT'

mainClassName = "uk.gov.bis.lite.customer.CustomerApplication"

repositories {
  mavenCentral()
  maven { url "http://nexus.mgmt.int.licensing.service.trade.gov.uk/repository/maven-releases/" }
}

dependencies {
  compile 'io.dropwizard:dropwizard-core:1.0.3'
  compile 'uk.gov.bis.lite:spire-client:1.1'
  compile 'ru.vyarus:dropwizard-guicey:3.2.0'
  compile 'io.dropwizard:dropwizard-testing:1.0.3'

  testCompile 'io.dropwizard:dropwizard-testing:1.0.3'
  testCompile 'org.assertj:assertj-core:3.4.1'
  testCompile group: 'junit', name: 'junit', version: '4.12'
}

// gradle liteCustomerServiceApiJar
task liteCustomerServiceApiJar(type: Jar, dependsOn: compileJava) {
  from sourceSets.main.output.classesDir
  include 'uk/gov/bis/lite/customer/api/**/*'
  baseName 'liteCustomerServiceApi'
}

// gradle publish
publishing {
  publications {
    api(MavenPublication) {
      groupId 'uk.gov.bis.lite'
      artifactId 'lite-customer-service-api'
      version '1.0'
      artifact liteCustomerServiceApiJar
    }
  }

  repositories {
    maven {
      name "snapshots"
      url "http://nexus.mgmt.int.licensing.service.trade.gov.uk/repository/maven-snapshots/"
    }
    maven {
      name "releases"
      url "http://nexus.mgmt.int.licensing.service.trade.gov.uk/repository/maven-releases/"
    }
  }
}

shadowJar {
  classifier = ''
  mergeServiceFiles()
}

jar {
  manifest {
    attributes 'Main-Class': mainClassName
  }
}

build {
  dependsOn shadowJar
}

run { args 'server', 'example.yml' }

